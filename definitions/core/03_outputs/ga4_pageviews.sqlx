   /*
  This file is part of "GMP Clearview" package.
  Copyright (C)  2025 Infotrust 
  Alina Zilbergerts, Trish Dothkar,
  -- */

  config {
    type: "incremental",
    tags: [dataform.projectConfig.vars.GA4_DATASET, "pages", "prod"],
    database: dataform.projectConfig.vars.OUTPUT_PROJECT,
    schema: dataform.projectConfig.vars.TRANSFORMATIONS_DATASET,
    name: "ga4_pageviews",
    description: "Table containing all captured page views",
    bigquery: {
        partitionBy: "event_date", // To improve query performance and control costs. You can only partition based on one dimension.
        clusterBy: ["event_date", "session_id"]
    }
}

js {
    const {
        helpers
    } = require("includes/core/helpers");
    const config = helpers.getConfig();
    /* check if there's invalid columns or dupe columns in the custom column definitions */
    helpers.checkColumnNames(config);

      var dateFilter = "" ;
  if (dataform.projectConfig.vars.RUN_FOR_DATE != undefined) {
    dateFilter = `event_date = date_checkpoint `;
  } else { 
    dateFilter = `event_date >= date_checkpoint `;
  }
}

  -- ==============================
  --        PRE-OPERATIONS
  -- ==============================
pre_operations {
  declare date_checkpoint DATE;
    ---  // this triple dash means something in dataform?

  -- this is for when we need to refill for certain date
  IF '${dataform.projectConfig.vars.RUN_FOR_DATE}' <> 'undefined' THEN 
    set date_checkpoint = (
      ${when(incremental(), 
          `select date('${dataform.projectConfig.vars.RUN_FOR_DATE}')`, 
          `select date('${config.GA4_START_DATE}')`
           )
      });
      -- delete some older data, since this may be updated later by GA4
      ${
        when(incremental(),
          `delete from ${self()} where event_date = date_checkpoint;`
          )
      }
  ELSE
      set date_checkpoint = (
        ${when(incremental(),
        `select coalesce(max(event_date)+1, date('${config.GA4_START_DATE}'))
            from ${self()} where is_final is True`,
        `select date('${config.GA4_START_DATE}')`)} 
      );
      -- delete some older data, since this may be updated later by GA4
      ${
        when(incremental(),
          `delete from ${self()} where event_date >= date_checkpoint;`
          )
      }
    END IF;
}

  -- ==============================
  --        POST-OPERATIONS
  -- ==============================
post_operations {
    ${when(incremental(),
    `EXPORT DATA OPTIONS(
        uri=(CONCAT('gs://${config.GCS_BUCKET}/${dataform.projectConfig.vars.TRANSFORMATIONS_DATASET}/',  FORMAT_DATE('%Y%m%d' , date_checkpoint), '_gcp_', '${name()}', '_*.parquet')), 
        format='PARQUET',
        overwrite=true
       ) AS

          SELECT DISTINCT * FROM (        -- Overhead SELECT DISTINCT and LIMIT are aded to fix the issue with producting multiple export files insteand of one
          SELECT * FROM ${self()} WHERE event_date = date_checkpoint
          )
          ORDER BY _run_timestamp LIMIT 1000000000000000000; `
    )}
}

  -- ==============================
  --            QUERY
  -- ==============================
with query_source as (
SELECT
  property_id,
  stream_id,
  event_date,
  event_timestamp,
  batch.batch_event_index,
  -- create page ID
--   farm_fingerprint(
--     time.event_timestamp ||
--     coalesce(user_pseudo_id, 'no') ||
--     coalesce(event_params.ga_session_id, 0) ||
--     coalesce(batch.batch_page_id, 0) ||
--     coalesce(batch.batch_ordering_id,0) ||
--     coalesce(batch.batch_event_index,0) ||
--     coalesce(page.location, "")
-- ) as page_id,
  event_id,
  session_id,
  

  -- pack user info
  STRUCT (
    user_pseudo_id,
    user_id,
    CASE 
      WHEN event_params.ga_session_number = 1 THEN 'new user'
      WHEN event_params.ga_session_number > 1 THEN 'returning user'
      ELSE NULL
    END AS user_type,
    is_active_user
  ) as user,
  device,
  geo,
  app_info,
  platform,

  session_traffic_source_last_click.cross_channel_campaign,

   STRUCT(
    page.location,
    page.path,
    page.title,
    page.hostname,
    page.referrer,
    event_params.content_group,
    event_params.content_id,
    event_params.content_type) 
  as page,
  -- METRICS

  -- Interaction metrics
  event_params.entrances,
  IF(LEAD(TIMESTAMP_MICROS(event_timestamp)) OVER (   
      PARTITION BY user_pseudo_id, session_id
      ORDER BY  event_timestamp
    ) IS NULL, 1, NULL) exits,

  -- logical_or(event_params.session_engaged = 1) OVER (PARTITION BY user_pseudo_id, session_id) as is_engaged_session,
  session_info.engaged_session_id,
  event_params.engagement_time_msec,

    -- User engagment metrics
  TIMESTAMP_DIFF(LEAD(TIMESTAMP_MICROS(event_timestamp)) OVER (   
      PARTITION BY user_pseudo_id, session_id
      ORDER BY  event_timestamp
    ) , TIMESTAMP_MICROS(event_timestamp) , SECOND) AS time_on_page_seconds,
  batch,  
  ROW_NUMBER() OVER 
      (PARTITION BY event_date, session_id ORDER BY event_timestamp ASC) AS page_rank_asc,
  ROW_NUMBER() OVER 
      (PARTITION BY event_date, session_id ORDER BY event_timestamp DESC) AS page_rank_desc,

  is_final 
FROM
  ${ref("base_ga4_events")}
WHERE  ${dateFilter}    --event_date >= date_checkpoint 
  AND event_name = 'page_view'
-- only hits with user_pseudo_id and session_id
  -- and user_pseudo_id is not null
  -- and session_id is not null

)

select
  current_timestamp() as _run_timestamp, -- indicates when the row was last run
  *
from query_source
where true