 /*
  This file is part of "GMP Clearview" package.
  Copyright (C)  2025 Infotrust 
  Alina Zilbergerts, Trish Dothkar,
  -- */

config {
  type: "incremental",
  //materialized: true ,
  tags: [dataform.projectConfig.vars.GA4_DATASET,"navigation","prod"],
  database: dataform.projectConfig.vars.OUTPUT_PROJECT,
  schema: dataform.projectConfig.vars.REPORTING_DATASET,
  name:"ga4_navigation",
  description: "Navigation events summary table with metrics",
  bigquery: {
    partitionBy: "event_date",
    //clusterBy: ["event_name", "event_id"],
    labels: require("includes/core/helpers.js").helpers.storageLabels()
  },
    columns: require("includes/core/documentation/helpers.js").ga4Events
}


js {
    const { helpers } = require("includes/core/helpers");
    const config = helpers.getConfig();

    var dateFilter = "" ;
  if (dataform.projectConfig.vars.RUN_FOR_DATE != "") {
    dateFilter = ` event_date = date_checkpoint `;
  } else { 
    dateFilter = ` event_date >= date_checkpoint `;
  }
}

  -- ==============================
  --            PRE-OPERATIONS 
  -- ==============================
/* incrementality */
pre_operations {
  declare date_checkpoint DATE;
    ---  // this triple dash means something in dataform?

  -- this is for when we need to refill for certain date
  IF '${dataform.projectConfig.vars.RUN_FOR_DATE}' <> 'undefined' THEN 
    set date_checkpoint = (
      ${when(incremental(), 
          `select date('${dataform.projectConfig.vars.RUN_FOR_DATE}')`, 
          `select date('${config.GA4_START_DATE}')`
           )
      });
      -- delete some older data, since this may be updated later by GA4
      ${
        when(incremental(),
          `delete from ${self()} where event_date = date_checkpoint;`
          )
      }
  ELSE
      set date_checkpoint = (
        ${when(incremental(),
        `select coalesce(max(event_date)+1, date('${config.GA4_START_DATE}'))
            from ${self()} where is_final is True`,
        `select date('${config.GA4_START_DATE}')`)} 
      );
      -- delete some older data, since this may be updated later by GA4
      ${
        when(incremental(),
          `delete from ${self()} where event_date >= date_checkpoint;`
          )
      }
    END IF;
}

  -- ==============================
  --        POST-OPERATIONS
  -- ==============================
post_operations {
    ${when(incremental(),
    `EXPORT DATA OPTIONS(
        uri=(CONCAT('gs://${config.GCS_BUCKET}/',  FORMAT_DATE('%Y%m%d' , date_checkpoint), '_gcp_', '${name()}', '_*.parquet')), 
        format='PARQUET',
        overwrite=true
       ) AS

          SELECT DISTINCT * FROM (        -- Overhead SELECT DISTINCT and LIMIT are aded to fix the issue with producting multiple export files insteand of one
          SELECT * FROM ${self()} WHERE event_date = date_checkpoint
          )
          ORDER BY _run_timestamp LIMIT 1000000000000000000; `
    )}
}


SELECT current_timestamp() as _run_timestamp, -- indicates when the row was last run
        * EXCEPT(session_id, user_pseudo_id, engagement_time_msec,
                outbound_general_link_click_count, outbound_tel_link_click_count,
                outbound_mailto_link_click_count,outbound_social_link_click_count ),

      COUNT(DISTINCT session_id) sessions,
      COUNT(DISTINCT user_pseudo_id) users,
      SUM(outbound_general_link_click_count) outbound_general_link_click_count,
      SUM(outbound_tel_link_click_count) outbound_tel_link_click_count,
      SUM(outbound_mailto_link_click_count) outbound_mailto_link_click_count,
      SUM(outbound_social_link_click_count) outbound_social_link_click_count,
      SUM(engagement_time_msec)/1000 engagement_time_sec,

FROM (
      SELECT * FROM (
        SELECT  
        event_date, event_name, 
        continent, country, region, city, page_location, cross_channel_campaign.default_channel_group,
        location, header,carousel_action,
        outbound_link_type, link_format,
        click_text, click_url, cta_text, cta_url,
        session_id, user_pseudo_id, 

      -- Metrics
        outbound_general_link_click_count, outbound_tel_link_click_count,
        outbound_mailto_link_click_count, outbound_social_link_click_count,
        engagement_time_msec,
        is_final


        FROM ${ref("ga4_engagements")}
        WHERE  ${dateFilter}            --event_date >= date_checkpoint
      )
        PIVOT 
        (
          COUNT(event_name) FOR event_name IN ('cta_click', 'click', 'navigation_click', "carousel_click", "accordion_click", 
          "provider_locator_cta_click", "provider_locator_get_directions_click", "provider_locator_tel_click", "internal_link_click")
        )
)
group by all
